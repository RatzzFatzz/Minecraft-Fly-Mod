plugins {
    id 'maven-publish'
    id 'fabric-loom' version '0.5.9'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = 'flymod'
version = '1.4.3'
group = 'at.pcgf.flymod'

ext {
    minecraft_version = "1.16.3"
    yarn_mappings = "1.16.3+build.10"
    loader_version = "0.9.3+build.207"
    fabric_version = "0.21.0+build.407-1.16"
    cloth_config_version = "4.8.1"
    mod_menu_version = "1.14.6+build.31"
    mixin_version = "0.8.11"
    clothevents_version = "1.4.8"
}

dependencies {
    minecraft(
            [group: 'com.mojang', name: 'minecraft', version: "${minecraft_version}"]
    )

    mappings(
            [group: 'net.fabricmc', name: 'yarn', version: "${yarn_mappings}"]
    )

    modCompile(
            [group: 'net.fabricmc', name: 'fabric-loader', version: "${loader_version}"],
            [group: 'me.shedaniel.cloth', name: 'config-2', version: "${cloth_config_version}"],
            [group: 'net.fabricmc.fabric-api', name: 'fabric-api', version: "${fabric_version}"],
            [group: 'net.fabricmc', name: 'sponge-mixin', version: "${mixin_version}"],
            [group: 'io.github.prospector', name: 'modmenu', version: "${mod_menu_version}"]
    )

    include(
            [group: 'me.shedaniel.cloth', name: 'config-2', version: "${cloth_config_version}"],
            [group: 'net.fabricmc.fabric-api', name: 'fabric-api', version: "${fabric_version}"],
            [group: 'net.fabricmc', name: 'sponge-mixin', version: "${mixin_version}"]
    )
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}